#
# A theme based on steeef theme
#  * Remote Git branch on the right
#  * Local Git branch info on the left
#  * Dual line prompt
#
# Authors:
#   Steve Losh <steve@stevelosh.com>
#   Bart Trojanowski <bart@jukie.net>
#   Brian Carper <brian@carper.ca>
#   steeef <steeef@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#   Yan Pritzker <yan@pritzker.ws>
#   Glenn Cueto <glenncueto@gmail.com>

function prompt_glnster_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info on
    git-info
  fi
}

function prompt_glnster_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook

  add-zsh-hook precmd prompt_glnster_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    __PROMPT_glnster_COLORS=(
      "%F{81}"  # turquoise
      "%F{166}" # orange
      "%F{135}" # purple
      "%F{161}" # hotpink
      "%F{118}" # limegreen
    )
  else
    __PROMPT_glnster_COLORS=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # git
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format "${__PROMPT_glnster_COLORS[1]}%b%f"
  zstyle ':prezto:module:git:info:added' format "${__PROMPT_glnster_COLORS[5]}+%f"
  zstyle ':prezto:module:git:info:deleted' format "${__PROMPT_glnster_COLORS[2]}×%f"
  zstyle ':prezto:module:git:info:modified' format "${__PROMPT_glnster_COLORS[4]}✗%f"
  zstyle ':prezto:module:git:info:ahead' format "${__PROMPT_glnster_COLORS[2]}↑%f"
  zstyle ':prezto:module:git:info:behind' format "${__PROMPT_glnster_COLORS[2]}↓%f"
  zstyle ':prezto:module:git:info:stashed' format "${__PROMPT_glnster_COLORS[2]}✭%f"
  zstyle ':prezto:module:git:info:unmerged' format "${__PROMPT_glnster_COLORS[2]}═%f"
  zstyle ':prezto:module:git:info:untracked' format "${__PROMPT_glnster_COLORS[3]}-%f"
  zstyle ':prezto:module:git:info:remote' format "${__PROMPT_glnster_COLORS[4]}⌁%R%f"
  zstyle ':prezto:module:git:info:keys' format \
    'prompt' '(%b%A%B%d%a%m%u)' \
    'rprompt' '(%R)'

#user: ${__PROMPT_glnster_COLORS[3]}%n%f@

  PROMPT="
${__PROMPT_glnster_COLORS[2]}%m%f ❯ ${__PROMPT_glnster_COLORS[5]}%~%f "'$git_info[prompt]'"
$ "
}
  RPROMPT='$git_info[rprompt]'

prompt_glnster_setup "$@"